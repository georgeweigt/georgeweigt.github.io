-- Feynman and Hibbs problem 3-12

-- Determine coefficients A, B, and C

W = i m omega / (2 hbar sin(omega T)) *
    ((x^2 + xc^2) cos(omega T) - 2 x xc) -
    m omega / (2 hbar) (xc - a)^2

C = eval(W, xc, 0)

B = eval(W - C, xc^2, 0)

A = W - B - C

A = eval(A, xc, 1)
B = eval(B, xc, 1)

"Verify coefficient A"

check(A == i m omega exp(i omega T) / (2 hbar sin(omega T)))

"ok"

"Verify coefficient B"

check(B == m omega / hbar (a - i x / sin(omega T)))

"ok"

"Verify coefficient C"

check(C == m omega / (2 hbar) (i x^2 cos(omega T) / sin(omega T) - a^2))

"ok"

"Verify equation (6)"

check(-pi / A == 2 pi i hbar sin(omega T) / (m omega exp(i omega T)))

"ok"

"Verify equation (7)"

check(-B^2 / (4 A) + C ==
-m omega / (2 hbar) *
(x^2 - 2 a x exp(-i omega T) + a^2 cos(omega T) exp(-i omega T)))

"ok"

"Verify equation (8)"

check(m omega / (2 pi i hbar sin(omega T)) (-pi / A) == exp(-i omega T))

"ok"
