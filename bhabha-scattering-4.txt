-- Compute R-squared for Bhabha scattering data from SLAC-PUB-1501

N = 12 -- number of observations

-- x is cos(theta)

x = (
0.6,
0.5,
0.4,
0.3,
0.2,
0.1,
0.0,
-0.1,
-0.2,
-0.3,
-0.4,
-0.5,
-0.6)

-- y is count data

y = (
4432,
2841,
2045,
1420,
1136,
852,
656,
625,
511,
455,
402,
398)

"Observed counts per bin"

y

-- T is total counts

T = sum(k,1,N,y[k])

-- f is unnormalized probability density function

f = (cos(theta)^2 + 3)^2 sin(theta) / sin(theta/2)^4

-- I integrates f from a to b

I(a,b) = sum(k,1,100,eval(f,theta,a + (k - 0.5) (b - a) / 100)) *
         (b - a) / 100

-- C is normalization constant

a = arccos(x[1])
b = arccos(x[N + 1])
C = I(a,b)

yhat = zero(N)

for(k,1,N,
  theta1 = arccos(x[k]),
  theta2 = arccos(x[k + 1]),
  P = 1/C I(theta1,theta2), -- P is probability
  yhat[k] = P T
)

"Predicted counts"

yhat

"Coefficient of determination (R-squared)"

ybar = 1/N sum(k,1,N,y[k])

SSE = sum(k,1,N,(y[k] - yhat[k])^2)
SST = sum(k,1,N,(y[k] - ybar)^2)

1 - SSE/SST
