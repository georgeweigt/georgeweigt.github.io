psi(n) = 1 / sqrt(2^n n!) (m omega / (pi hbar))^(1/4) *
         H(n, x sqrt(m omega / hbar)) *
         exp(-m omega x^2 / (2 hbar))

-- Hermite polynomial

H(n,y,z) = (-1)^n exp(y^2) eval(d(exp(-z^2),z,n),z,y)

"Wave function"

psi1 = eval(psi(1), x, xa)
psi1

-- propagator

K = sqrt(m omega / (2 pi i hbar sin(omega T))) *
    exp(i m omega / (2 hbar sin(omega T)) *
    (xa^2 cos(omega T) - 2 xa xb + xb^2 cos(omega T)))

-- decomposition of K psi1

A = m omega / (2 hbar) -
    i m omega cos(omega T) / (2 hbar sin(omega T))

B = -i m omega xb / (hbar sin(omega T))

C = i m omega xb^2 cos(omega T) / (2 hbar sin(omega T))

D = -m^(5/4) omega^(5/4) / ((-1)^(1/4) hbar^(5/4) pi^(3/4) *
     sin(omega T)^(1/2))

-- check decomposition

check(K psi1 == D xa exp(-A xa^2 + B xa + C))

"Path integral"

I = D sqrt(pi / A) B / (2 A) exp(B^2 / (4 A) + C)
I

"Substitute for T"

I = eval(I, T, pi / (2 omega))
I

"Verify result"

check(I == eval(psi1, xa, xb) exp(i pi / 4))

"ok"
