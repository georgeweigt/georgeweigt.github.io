"Laplace transform example"

Ca(s) = (A Cb(s + i omega) + 1) / s
T = A Cb(s) / (s - i omega) + 1 / (s - i omega)
check(Ca(s - i omega) == T)

X = B / (s - i omega)
Y = s - A B / (s - i omega)

Cb = X / Y

a = -i omega
b = -A B

check(Cb == B / (s^2 + a s + b))

k = sqrt(4 b - a^2)

-- inverse Laplace transform

T = 2/k sin(1/2 k t) exp(-1/2 a t)

cb = B T
ca = d(cb,t) / (B exp(i omega t))

ca
cb

-- verify solutions

X = cos(1/2 k t) exp(-1/2 i omega t) +
    i omega / k sin(1/2 k t) exp(-1/2 i omega t)

Y = 2 B / k sin(1/2 k t) exp(1/2 i omega t)

check(ca == X)
check(cb == Y)

check(d(ca,t) == A exp(-i omega t) cb)
check(d(cb,t) == B exp(i omega t) ca)

check(eval(ca,t,0) == 1)
check(eval(cb,t,0) == 0)

"ok"
