"Laplace transform example"

Ca(s) = (A Cb(s + i omega) + i hbar) / (i hbar s)
T = -i A Cb(s) / (hbar (s - i omega)) + 1 / (s - i omega)
check(Ca(s - i omega) == T)

X = B / (s - i omega)
Y = i hbar s + i A B / (hbar (s - i omega))

Cb = X / Y

a = -i omega
b = A B / hbar^2

check(Cb == -i B / hbar / (s^2 + a s + b))

k = sqrt(4 b - a^2)

-- inverse Laplace transform

T = 2 sin(k t / 2) exp(-a t / 2) / k

cb = -i B / hbar T
ca = i hbar d(cb,t) / (B exp(i omega t))

ca
cb

-- verify solutions

X = cos(k t / 2) exp(-i omega t / 2) +
    i omega / k sin(k t / 2) exp(-i omega t / 2)

Y = -2 i B / (hbar k) sin(k t / 2) exp(i omega t / 2)

check(ca == X)
check(cb == Y)

check(i hbar d(ca,t) == A exp(-i omega t) cb)
check(i hbar d(cb,t) == B exp(i omega t) ca)

check(eval(ca,t,0) == 1)
check(eval(cb,t,0) == 0)

"ok"
