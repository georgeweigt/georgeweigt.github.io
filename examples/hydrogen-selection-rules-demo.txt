"Verify selection rules"

-- energy levels

ni = 3
nf = 2

-- wave function

psi(n,l,m) = R(n,l) Y(l,m)
 
R(n,l) = 2 / n^2 *
         a0^(-3/2) *
         sqrt((n - l - 1)! / (n + l)!) *
         (2 r / (n a0))^l *
         L(2 r / (n a0), n - l - 1, 2 l + 1) *
         exp(-r / (n a0))

-- associated Laguerre polynomial (k is a local var)

L(x,n,m,k) = (n + m)! sum(k, 0, n, (-x)^k / ((n - k)! (m + k)! k!))

-- spherical harmonic

Y(l,m) = (-1)^m sqrt((2 l + 1) / (4 pi) (l - m)! / (l + m)!) *
         P(l,m) exp(i m phi)

-- associated Legendre of cos theta (arxiv.org/abs/1805.12125)

P(l,m,k) = test(m < 0, (-1)^m (l + m)! / (l - m)! P(l,-m),
           (sin(theta)/2)^m sum(k, 0, l - m,
           (-1)^k (l + m + k)! / (l - m - k)! / (m + k)! / k! *
           ((1 - cos(theta)) / 2)^k))

-- integrator

I(f) = do(
 f = f r^2 sin(theta), -- multiply by volume element
 f = expform(f), -- convert to exponential form
 f = defint(f, theta, 0, pi, phi, 0, 2 pi),
 f = integral(f,r),
 0 - eval(f,r,0) -- return value
)

N = 0

for(li, 0, ni - 1, for(mi, -li, li,
for(lf, 0, nf - 1, for(mf, -lf, lf,
 psii = psi(ni,li,mi),
 psif = psi(nf,lf,mf),
 xif = I(conj(psif) psii r sin(theta) cos(phi)),
 yif = I(conj(psif) psii r sin(theta) sin(phi)),
 zif = I(conj(psif) psii r cos(theta)),
 R2 = conj(xif) xif + conj(yif) yif + conj(zif) zif,
 test(R2, do(
  check(abs(lf - li) == 1),
  check(abs(mf - mi) <= 1),
  N = N + 1))
))))

"ok"

"Number of allowed transitions"
N
