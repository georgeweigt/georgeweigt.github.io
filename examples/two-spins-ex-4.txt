"Exercise 4. Verify expectation of product S1x S2z."

sigmax = ((0,1),(1,0))
sigmay = ((0,-i),(i,0))
sigmaz = ((1,0),(0,-1))

I = ((1,0),(0,1))

S1x = 1/2 hbar kronecker(sigmax,I)
S1y = 1/2 hbar kronecker(sigmay,I)
S1z = 1/2 hbar kronecker(sigmaz,I)

S2x = 1/2 hbar kronecker(I,sigmax)
S2y = 1/2 hbar kronecker(I,sigmay)
S2z = 1/2 hbar kronecker(I,sigmaz)

s1 = (cos(1/2 theta1), expsin(1/2 theta1) exp(i phi1))
s2 = (cos(1/2 theta2), expsin(1/2 theta2) exp(i phi2))

s = kronecker(s1,s2)

ES1x = 1/2 hbar sin(theta1) cos(phi1)
ES1y = 1/2 hbar sin(theta1) sin(phi1)
ES1z = 1/2 hbar cos(theta1)

ES2x = 1/2 hbar sin(theta2) cos(phi2)
ES2y = 1/2 hbar sin(theta2) sin(phi2)
ES2z = 1/2 hbar cos(theta2)

check(dot(conj(s),S1x,S2z,s) == ES1x ES2z)

"ok"
