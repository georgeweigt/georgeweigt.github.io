"Stern-Gerlach 2"

N = 10

a = zero(N)

a[1] = 1 / (1.3541179394 3^(2/3))
a[2] = -1 / (2.6789385347 3^(1/3))

for(n,4,N, a[n] = a[n - 3] / (n + 3 - 4) / (n + 2 - 4))

check(infixform(a[4]) == "0.0591713")
check(infixform(a[5]) == "-0.0215683")
check(a[6] == 0)
check(infixform(a[7]) == "0.00197238")
check(infixform(a[8]) == "-0.000513531")
check(a[9] == 0)

f = sum(n,0,N-1, a[n + 1] x^n)

C1 = (muB alpha m / hbar^2)^(1/3)
C2 = muB alpha / (4 m)

W1 = exp(-i alpha muB z t / (2 hbar))
W2 = exp(-i muB B0 t / hbar)

psi1 = eval(f, x, C1 C2 t^2 + C1 z) W1 W2
psi2 = eval(f, x, C1 C2 t^2 - C1 z) conj(W1) conj(W2)

epsilon1 = -hbar^2 / (2 m) d(psi1,z,z) +
           muB (B0 + alpha z) psi1 -
           i hbar d(psi1,t)

epsilon2 = -hbar^2 / (2 m) d(psi2,z,z) -
           muB (B0 + alpha z) psi2 -
           i hbar d(psi2,t)

-- cancel exponentials

epsilon1 = epsilon1 conj(W1) conj(W2)
epsilon2 = epsilon2 W1 W2

-- truncate

for(n, N - 2, 2 N,
 epsilon1 = eval(epsilon1, t^n, 0),
 epsilon2 = eval(epsilon2, t^n, 0)
)

for(n, N / 2, N,
 epsilon1 = eval(epsilon1, z^n, 0),
 epsilon2 = eval(epsilon2, z^n, 0)
)

epsilon1
epsilon2
