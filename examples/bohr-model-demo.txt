"Bohr model"

E = -alpha^2 m c^2 / (2 n^2)
v = alpha c / n
r = n^2 hbar / (alpha m c)
check(E == -1/2 m v^2)
check(m v r == n hbar)

alpha = e^2 / (4 pi epsilon0 hbar c)
check(E == -m e^4 / (2 (4 pi epsilon0)^2 hbar^2 n^2))
check(r == 4 pi epsilon0 hbar^2 n^2 / (m e^2))

-- CODATA Internationally recommended 2022 values
-- https://physics.nist.gov/cuu/Constants/

a0 = 5.29177210544 10^(-11) meter
alpha = 7.2973525643 10^(-3)
c = 299792458.0 meter / second
e = 1.602176634 10^(-19) coulomb
epsilon0 = 8.8541878188 10^(-12) farad / meter
h = 6.62607015 10^(-34) joule second
hbar = h / float(2 pi)
k = 1.380649 10^(-23) joule / kelvin
me = 9.1093837139 10^(-31) kilogram
mp = 1.67262192595 10^(-27) kilogram
mu0 = 1.25663706127 10^(-6) newton / ampere^2

-- derived units

coulomb = ampere second
farad = coulomb / volt
joule = kilogram meter^2 / second^2
newton = kilogram meter / second^2
tesla = kilogram / second^2 / ampere
volt = joule / coulomb

-- base units

ampere = "ampere"
kelvin = "kelvin"
kilogram = "kilogram"
meter = "meter"
second = "second"

-- eV per joule

eV = 1/e coulomb / joule "eV"

E1 = -1/2 alpha^2 me c^2 eV
r1 = hbar / (alpha me c)

E1
r1

"Reduced electron mass"

mu = me mp / (me + mp)

E1 = -1/2 alpha^2 mu c^2 eV
r1 = hbar / (alpha mu c)

E1
r1
