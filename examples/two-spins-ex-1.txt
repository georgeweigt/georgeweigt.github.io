"Exercise 1. Verify spin operators for two spins."

sigmax = ((0,1),(1,0))
sigmay = ((0,-i),(i,0))
sigmaz = ((1,0),(0,-1))

I = ((1,0),(0,1))

S1x = 1/2 hbar kronecker(sigmax,I)
S1y = 1/2 hbar kronecker(sigmay,I)
S1z = 1/2 hbar kronecker(sigmaz,I)

S2x = 1/2 hbar kronecker(I,sigmax)
S2y = 1/2 hbar kronecker(I,sigmay)
S2z = 1/2 hbar kronecker(I,sigmaz)

check(S1x == 1/2 hbar ((0,0,1,0),(0,0,0,1),(1,0,0,0),(0,1,0,0)))
check(S1y == 1/2 hbar ((0,0,-i,0),(0,0,0,-i),(i,0,0,0),(0,i,0,0)))
check(S1z == 1/2 hbar ((1,0,0,0),(0,1,0,0),(0,0,-1,0),(0,0,0,-1)))

check(S2x == 1/2 hbar ((0,1,0,0),(1,0,0,0),(0,0,0,1),(0,0,1,0)))
check(S2y == 1/2 hbar ((0,-i,0,0),(i,0,0,0),(0,0,0,-i),(0,0,i,0)))
check(S2z == 1/2 hbar ((1,0,0,0),(0,-1,0,0),(0,0,1,0),(0,0,0,-1)))

-- total spin

Sx = S1x + S2x
Sy = S1y + S2y
Sz = S1z + S2z

S2 = dot(Sx,Sx) + dot(Sy,Sy) + dot(Sz,Sz)

check(S2 == hbar^2 ((2,0,0,0),(0,1,1,0),(0,1,1,0),(0,0,0,2)))

"ok"
