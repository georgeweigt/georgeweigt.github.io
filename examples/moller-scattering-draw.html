<html><body><script src='https://georgeweigt.github.io/eigenmath.js'></script><textarea id='stdin' style='width:800px;height:400px;font-family:courier;font-size:12pt'>
-- Draw pdf cdf for Moller scattering

N = 12 -- number of bins

pi = float(pi) -- use numerical value of pi
a = pi / N
I = -8 cos(theta) / sin(theta)^2 - cos(theta)
F = (I - eval(I,theta,a)) / (eval(I,theta,pi-a) - eval(I,theta,a))
f = d(F,theta)

"Probability density function"

xrange = (0,pi)
yrange = (0,1)
draw(f,theta)

"Cumulative distribution function"

xrange = (0,pi)
yrange = (0,1)
draw(F,theta)

"Bin probability (first and last not observed)"

P = zero(N)
for(k,2,N-1, P[k] = eval(F,theta,k pi/N) - eval(F,theta,(k-1) pi/N))
h(x) = test(x <= 0, 0, x > N, 0, P[ceiling(x)])
xrange = (0,N)
yrange = (0,1)
draw(h,x)
</textarea> <button onclick='run()'>Run</button><br><div id='stdout'></div></body></html>
