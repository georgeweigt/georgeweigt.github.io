<html><head><link rel='stylesheet' href='../style.css'></head><body><script src='https://georgeweigt.github.io/eigenmath.js'></script><textarea id='stdin'>
-- Spin state

sigmax = ((0,1),(1,0))
sigmay = ((0,-i),(i,0))
sigmaz = ((1,0),(0,-1))

sigma = (sigmax,sigmay,sigmaz)

"Verify spin commutation relations"
check(dot(sigmax,sigmay) - dot(sigmay,sigmax) == 2 i sigmaz)
check(dot(sigmay,sigmaz) - dot(sigmaz,sigmay) == 2 i sigmax)
check(dot(sigmaz,sigmax) - dot(sigmax,sigmaz) == 2 i sigmay)
"ok"

"Verify spin direction vector"
s = (cos(theta/2), sin(theta/2) exp(i phi))
u = (sin(theta) cos(phi), sin(theta) sin(phi), cos(theta))
check(dot(conj(s),transpose(sigma),s) == u)
"ok"

"Verify example spin"

s = (1/3 - 2/3 i, 2/3)

x = dot(conj(s),sigmax,s)
y = dot(conj(s),sigmay,s)
z = dot(conj(s),sigmaz,s)

check(x == 4/9)
check(y == 8/9)
check(z == 1/9)

c1 = sqrt((z + 1) / 2)
c2 = sqrt((1 - z) / 2) (x + i y) / sqrt(x^2 + y^2)

check(c1 == sqrt(5) / 3)
check(c2 == (2 + 4 i) / (3 sqrt(5)))

s = (c1,c2)

check(dot(conj(s),sigmax,s) == 4/9)
check(dot(conj(s),sigmay,s) == 8/9)
check(dot(conj(s),sigmaz,s) == 1/9)

"ok"
</textarea> <button onclick='run()'>Run</button><br><div id='stdout'></div></body></html>
