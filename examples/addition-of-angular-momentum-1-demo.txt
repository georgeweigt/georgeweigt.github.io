"Addition of angular momentum"

Lx(f) = -i hbar (y d(f,z) - z d(f,y))
Ly(f) = -i hbar (z d(f,x) - x d(f,z))
Lz(f) = -i hbar (x d(f,y) - y d(f,x))

L(f) = (Lx(f),Ly(f),Lz(f))
L2(f) = Lx(Lx(f)) + Ly(Ly(f)) + Lz(Lz(f))

LxL(f) = (Ly(Lz(f)) - Lz(Ly(f)),
          Lz(Lx(f)) - Lx(Lz(f)),
          Lx(Ly(f)) - Ly(Lx(f)))

Sx(f) = hbar / 2 dot(((0,1),(1,0)),f)
Sy(f) = hbar / 2 dot(((0,-i),(i,0)),f)
Sz(f) = hbar / 2 dot(((1,0),(0,-1)),f)

S(f) = (Sx(f),Sy(f),Sz(f))
S2(f) = Sx(Sx(f)) + Sy(Sy(f)) + Sz(Sz(f))

SxS(f) = (Sy(Sz(f)) - Sz(Sy(f)),
          Sz(Sx(f)) - Sx(Sz(f)),
          Sx(Sy(f)) - Sy(Sx(f)))

Jx(f) = Lx(f) + Sx(f)
Jy(f) = Ly(f) + Sy(f)
Jz(f) = Lz(f) + Sz(f)

J(f) = (Jx(f),Jy(f),Jz(f))
J2(f) = Jx(Jx(f)) + Jy(Jy(f)) + Jz(Jz(f))

JxJ(f) = (Jy(Jz(f)) - Jz(Jy(f)),
          Jz(Jx(f)) - Jx(Jz(f)),
          Jx(Jy(f)) - Jy(Jx(f)))

# theta and phi are the spin direction

chi = (c1(theta,phi),
       c2(theta,phi))

Psi = psi(x,y,z) chi

check(LxL(Psi) == i hbar L(Psi))
check(SxS(Psi) == i hbar S(Psi))
check(JxJ(Psi) == i hbar J(Psi))

"Verify equation (1)"
LS(f) = contract(L(S(Psi)))
check(J2(Psi) == L2(Psi) + S2(Psi) + 2 LS(Psi))
"ok"

"Verify commutation relations"

check(J2(L2(Psi)) - L2(J2(Psi)) == 0)
check(J2(S2(Psi)) - S2(J2(Psi)) == 0)

check(J2(Jx(Psi)) - Jx(J2(Psi)) == 0)
check(J2(Jy(Psi)) - Jy(J2(Psi)) == 0)
check(J2(Jz(Psi)) - Jz(J2(Psi)) == 0)

check(J2(Lx(Psi)) - Lx(J2(Psi)) == 2 i hbar (Ly(Sz(Psi)) - Lz(Sy(Psi))))
check(J2(Ly(Psi)) - Ly(J2(Psi)) == 2 i hbar (Lz(Sx(Psi)) - Lx(Sz(Psi))))
check(J2(Lz(Psi)) - Lz(J2(Psi)) == 2 i hbar (Lx(Sy(Psi)) - Ly(Sx(Psi))))

check(J2(Sx(Psi)) - Sx(J2(Psi)) == -2 i hbar (Ly(Sz(Psi)) - Lz(Sy(Psi))))
check(J2(Sy(Psi)) - Sy(J2(Psi)) == -2 i hbar (Lz(Sx(Psi)) - Lx(Sz(Psi))))
check(J2(Sz(Psi)) - Sz(J2(Psi)) == -2 i hbar (Lx(Sy(Psi)) - Ly(Sx(Psi))))

check(L2(Lx(Psi)) - Lx(L2(Psi)) == 0)
check(L2(Ly(Psi)) - Ly(L2(Psi)) == 0)
check(L2(Lz(Psi)) - Lz(L2(Psi)) == 0)

check(Lx(Ly(Psi)) - Ly(Lx(Psi)) == i hbar Lz(Psi))
check(Ly(Lz(Psi)) - Lz(Ly(Psi)) == i hbar Lx(Psi))
check(Lz(Lx(Psi)) - Lx(Lz(Psi)) == i hbar Ly(Psi))

check(S2(Sx(Psi)) - Sx(S2(Psi)) == 0)
check(S2(Sy(Psi)) - Sy(S2(Psi)) == 0)
check(S2(Sz(Psi)) - Sz(S2(Psi)) == 0)

check(Sx(Sy(Psi)) - Sy(Sx(Psi)) == i hbar Sz(Psi))
check(Sy(Sz(Psi)) - Sz(Sy(Psi)) == i hbar Sx(Psi))
check(Sz(Sx(Psi)) - Sx(Sz(Psi)) == i hbar Sy(Psi))

"ok"
