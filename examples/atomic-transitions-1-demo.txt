"Atomic transitions 1"

-- hydrogen wave function

psi(n,l,m) = R(n,l) Y(l,m)
 
R(n,l) = 2 / n^2 *
         a0^(-3/2) *
         sqrt((n - l - 1)! / (n + l)!) *
         (2 r / (n a0))^l *
         L(2 r / (n a0), n - l - 1, 2 l + 1) *
         exp(-r / (n a0))

-- associated Laguerre polynomial (k is a local var)

L(x,n,m,k) = (n + m)! sum(k, 0, n, (-x)^k / ((n - k)! (m + k)! k!))

-- spherical harmonic

Y(l,m) = (-1)^m sqrt((2 l + 1) / (4 pi) (l - m)! / (l + m)!) *
         P(l,m) exp(i m phi)

-- associated Legendre of cos theta (arxiv.org/abs/1805.12125)

P(l,m,k) = test(m < 0, (-1)^m (l + m)! / (l - m)! P(l,-m),
           (sin(theta)/2)^m sum(k, 0, l - m,
           (-1)^k (l + m + k)! / (l - m - k)! / (m + k)! / k! *
           ((1 - cos(theta)) / 2)^k))

E(n) = -hbar^2 / (2 n^2 mu a0^2)

-- for example, energy levels 2 and 3

na = 2
nb = 3

psia = psi(na,0,0)
psib = psi(nb,0,0)

Ea = E(na)
Eb = E(nb)

Psi = psia ca(t) exp(-i/hbar Ea t) + psib cb(t) exp(-i/hbar Eb t)

-- time-independent Hamiltonian

H0(f) = -hbar^2 D(f) / (2 mu) - hbar^2 / (mu a0 r) f

-- Laplacian

D(f) = 1/r^2 d(r^2 d(f,r), r) +
       1/(r^2 sin(theta)) d(sin(theta) d(f,theta), theta) +
       1/(r sin(theta))^2 d(f,phi,2)

-- left side of Schrodinger equation

A = i hbar d(Psi, t)

-- right side of Schrodinger equation

B = H0(Psi) + H1(Psi)

-- the part that cancels

E = Ea psia ca(t) exp(-i/hbar Ea t) + Eb psib cb(t) exp(-i/hbar Eb t)

-- the part that remains

T = i hbar psia d(ca(t),t) exp(-i/hbar Ea t) +
    i hbar psib d(cb(t),t) exp(-i/hbar Eb t)

"Verify equation (1)"
check(A - E == T)
check(B - E == H1(Psi))
"ok"
