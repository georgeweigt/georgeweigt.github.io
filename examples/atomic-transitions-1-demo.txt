"Atomic transitions 1"

H1 = -e E0 epsilon p / (m omega) expcos(k r - omega t)

cb = -i / hbar defint(H1 exp(i omega0 t), t, 0, t)

A = e E0 / (2 m hbar omega) epsilon p exp(i k r) *
    (exp(i (omega0 - omega) t) - 1) / (omega0 - omega)

B = e E0 / (2 m hbar omega) epsilon p exp(-i k r) *
    (exp(i (omega0 + omega) t) - 1) / (omega0 + omega)

"Verify equation (1)"
check(cb == A + B)
"ok"

cb = A

"Verify equation (2)"
T = i e E0 / (m hbar omega) epsilon p exp(i k r) *
    sin(1/2 (omega0 - omega) t) / (omega0 - omega) *
    exp(i/2 (omega0 - omega) t)
check(cb == T)
"ok"

"Verify dimensions"

momentum = kilogram meter / second
newton = kilogram meter / second^2
joule = kilogram meter^2 / second^2

e = coulomb
E0 = newton / coulomb
hbar = joule second
m = kilogram
omega = second^(-1)
p = momentum

check(e E0 p / (m omega) == joule)
check(e E0 / (m hbar omega) p / omega == 1)

"ok"
