"Problem 4.18"

psi211 = -r sin(theta) / (8 sqrt(pi a0^5)) exp(-r / (2 a0) + i phi)
psi21m1 = r sin(theta) / (8 sqrt(pi a0^5)) exp(-r / (2 a0) - i phi)

Psi = 1 / sqrt(2) (psi211 + psi21m1) exp(-i E2 t / hbar)

V = -e^2 / (4 pi epsilon0 r)

"Verify equation (1)"
f = conj(Psi) V Psi r^2 sin(theta)
F = defint(f, phi, 0, 2 pi, theta, 0, pi)
I = integral(F,r)
Vbar = 0 - eval(I,r,0)
check(Vbar == -e^2 / (16 pi epsilon0 a0))
"ok"

-- physical constants (c, e, h, and k are exact values)

c = 299792458.0 meter / second                 -- speed of light in vacuum
e = 1.602176634 10^(-19) coulomb               -- elementary charge
epsilon0 = 8.8541878128 10^(-12) farad / meter -- vacuum electric permittivity
h = 6.62607015 10^(-34) joule second           -- Planck constant
hbar = h / float(2 pi)                         -- reduced Planck constant
k = 1.380649 10^(-23) joule / kelvin           -- Boltzmann constant
me = 9.1093837015 10^(-31) kilogram            -- electron mass
mp = 1.67262192369 10^(-27) kilogram           -- proton mass
mu = me mp / (me + mp)                         -- reduced electron mass

-- derived units

coulomb = ampere second
farad = coulomb / volt
joule = kilogram meter^2 / second^2
volt = joule / coulomb

-- base units (for printing)

ampere = "ampere"
kelvin = "kelvin"
kilogram = "kilogram"
meter = "meter"
second = "second"

electronvolt = e joule / coulomb -- convert electronvolt to joule

pi = float(pi)
a0 = 4 pi epsilon0 hbar^2 / (e^2 mu)
Vbar = Vbar / electronvolt "eV"
Vbar
