"Problem 11.4"

Z = alpha exp(i phi)

A = -i / hbar Z / epsilon
B = -i / hbar conj(Z) / epsilon

X = B / (s - i omega)
Y = s - A B / (s - i omega)

Cb = X / Y

a = -i omega
b = -A B

check(Cb == B / (s^2 + a s + b))

k = sqrt(4 b - a^2)

cb = B 2/k sin(1/2 k t) exp(-1/2 a t)

ca = d(cb,t) / (B exp(i omega t))

-- verify solutions

X = cos(1/2 k t) exp(-1/2 i omega t) +
    i omega / k sin(1/2 k t) exp(-1/2 i omega t)

Y = 2 B / k sin(1/2 k t) exp(1/2 i omega t)

check(ca == X)
check(cb == Y)

check(d(ca,t) == A exp(-i omega t) cb)
check(d(cb,t) == B exp(i omega t) ca)

check(eval(ca,t,0) == 1)
check(eval(cb,t,0) == 0)

ca = eval(ca, t, epsilon)
cb = eval(cb, t, epsilon)

ca
cb

-- trick that achieves the same result as epsilon = 0

ca = eval(ca, omega, 0)
cb = eval(cb, omega, 0)

ca
cb

check(ca == cos(mag(Z) / hbar))
check(cb == -i sin(mag(Z) / hbar) conj(Z) / mag(Z))

check(conj(ca) ca + conj(cb) cb == 1)

P = conj(cb) cb
P

"ok"
