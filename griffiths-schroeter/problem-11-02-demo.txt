"Problem 11.2"

psi(n,l,m) = R(n,l) Y(l,m)

R(n,l) = 2 / n^2 *
         a0^(-3/2) *
         sqrt((n - l - 1)! / (n + l)!) *
         (2 r / (n a0))^l *
         L(2 r / (n a0), n - l - 1, 2 l + 1) *
         exp(-r / (n a0))

-- associated Laguerre polynomial (k is a local var)

L(x,n,m,k) = (n + m)! sum(k, 0, n, (-x)^k / ((n - k)! (m + k)! k!))

-- spherical harmonic

Y(l,m) = (-1)^m sqrt((2 l + 1) / (4 pi) (l - m)! / (l + m)!) *
         P(l,m) exp(i m phi)

-- associated Legendre of cos theta (arxiv.org/abs/1805.12125)

P(l,m,k) = test(m < 0, (-1)^m (l + m)! / (l - m)! P(l,-m),
           (sin(theta)/2)^m sum(k, 0, l - m,
           (-1)^k (l + m + k)! / (l - m - k)! / (m + k)! / k! *
           ((1 - cos(theta)) / 2)^k))

-- integrate f

I(f) = do(
  f = f r^2 sin(theta), -- multiply by volume element
  f = expform(f), -- convert to exponential form
  f = defint(f,theta,0,pi,phi,0,2pi),
  f = integral(f,r),
  0 - eval(f,r,0) -- return value
)

X(fk,fi) = I(e E(t) conj(fk) r cos(theta) fi)

psi1 = psi(1,0,0)
psi2 = psi(2,0,0)
X(psi1,psi2)

psi2 = psi(2,1,0)
X(psi1,psi2)

psi2 = psi(2,1,-1)
X(psi1,psi2)

psi2 = psi(2,1,1)
X(psi1,psi2)

-- check Hii = 0

check(X(psi1,psi1) == 0)

psi2 = psi(2,1,0)
check(X(psi2,psi2) == 0)

psi2 = psi(2,1,-1)
check(X(psi2,psi2) == 0)

psi2 = psi(2,1,1)
check(X(psi2,psi2) == 0)

"ok"
