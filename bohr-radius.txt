-- E is total energy

E = hbar^2 / (2 me a0^2) - e^2 / (4 pi epsilon0 a0)

dE = d(E,a0)
dE

-- Bohr radius minimizes total energy

a0 = 4 pi epsilon0 hbar^2 / (e^2 me)

"Bohr radius"

a0

"Verify that energy is minimized for Bohr radius"

check(dE == 0)

"ok"

-- base units

ampere = "ampere"
kilogram = "kilogram"
meter = "meter"
second = "second"

-- derived units

coulomb = ampere second
farad = ampere^2 kilogram^(-1) meter^(-2) second^4
joule = kilogram meter^2 second^(-2)
volt = ampere^(-1) kilogram meter^2 second^(-3)

pi = float(pi) -- use numerical value of pi

h = 6.62607015 10^(-34) joule second           -- Planck constant (exact)
hbar = h / (2 pi)
e = 1.602176634 10^(-19) coulomb               -- elementary charge (exact)
eV = 1.602176634 10^(-19) joule / "eV"         -- electron volt (exact)
me = 9.1093837015 10^(-31) kilogram            -- electron mass
epsilon0 = 8.8541878128 10^(-12) farad / meter -- vacuum electric permittivity

"Numerical value for Bohr radius"

a0

"Numerical value for Rydberg unit of energy"

-E / eV
