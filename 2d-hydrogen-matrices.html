<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="UTF-8">
<title>Eigenmath Demo</title>
</head>
<body>

<script src="eigenmath.js"></script>

<table><tr><td>
<textarea id="stdin" rows="24" cols="80" style="font-family:courier;font-size:12pt">
-- Compute matrices for 2d hydrogen

E(n) = -k^2 mu / (2 hbar^2 (n + 1/2)^2)

e = quote(e) -- clear e

k = e^2 / (4 pi epsilon0)

H = ((E(1),0,0,0),
     (0,E(2),0,0),
     (0,0,E(3),0),
     (0,0,0,E(4)))

psi(n,m) =
  1 / a0 / sqrt(pi) *
  (n + 1/2)^(-3/2) *
  sqrt((n - abs(m))! / (n + abs(m))!) *
  (2 r / a0 / (n + 1/2))^abs(m) *
  Laguerre(2 r / a0 / (n + 1/2), n - abs(m), 2 abs(m)) *
  exp(-r / a0 / (n + 1/2)) *
  exp(i m phi)

a0 = hbar^2 / (k mu)

-- Laguerre polynomial (y is a local variable)

Laguerre(x,n,m,y) =
  eval(y^(-m) exp(y) / n! d(exp(-y) y^(n + m), y, n), y, x)

psi1 = psi(1,0)
psi2 = psi(2,0)
psi3 = psi(3,0)
psi4 = psi(4,0)

I(f) = do(
  f = defint(f r, phi, 0, 2 pi),
  f = integral(f,r),
  0 - eval(f,r,0)
)

phat(psi) = -hbar^2 (d(psi,r,2) + d(psi,r) / r + d(psi,phi,2) / r^2)

P2 = ((I(conj(psi1) phat(psi1)),
       I(conj(psi1) phat(psi2)),
       I(conj(psi1) phat(psi3)),
       I(conj(psi1) phat(psi4))),

      (I(conj(psi2) phat(psi1)),
       I(conj(psi2) phat(psi2)),
       I(conj(psi2) phat(psi3)),
       I(conj(psi2) phat(psi4))),

      (I(conj(psi3) phat(psi1)),
       I(conj(psi3) phat(psi2)),
       I(conj(psi3) phat(psi3)),
       I(conj(psi3) phat(psi4))),

      (I(conj(psi4) phat(psi1)),
       I(conj(psi4) phat(psi2)),
       I(conj(psi4) phat(psi3)),
       I(conj(psi4) phat(psi4))))

V = ((I(conj(psi1) 1/r psi1),
      I(conj(psi1) 1/r psi2),
      I(conj(psi1) 1/r psi3),
      I(conj(psi1) 1/r psi4)),

     (I(conj(psi2) 1/r psi1),
      I(conj(psi2) 1/r psi2),
      I(conj(psi2) 1/r psi3),
      I(conj(psi2) 1/r psi4)),

     (I(conj(psi3) 1/r psi1),
      I(conj(psi3) 1/r psi2),
      I(conj(psi3) 1/r psi3),
      I(conj(psi3) 1/r psi4)),

     (I(conj(psi4) 1/r psi1),
      I(conj(psi4) 1/r psi2),
      I(conj(psi4) 1/r psi3),
      I(conj(psi4) 1/r psi4)))

-- physical constants (h and kB are exact values)

c = 299792458.0 meter / second
e = 1.602176634 10^(-19) coulomb
epsilon0 = 8.8541878128 10^(-12) farad / meter
h = 6.62607015 10^(-34) joule second
hbar = h / float(2 pi)
kB = 1.380649 10^(-23) joule / kelvin
me = 9.1093837015 10^(-31) kilogram
mp = 1.67262192369 10^(-27) kilogram
mu = me mp / (me + mp)

-- derived units

coulomb = ampere second
farad = coulomb / volt
joule = kilogram meter^2 / second^2
volt = joule / coulomb

-- base units (for printing)

ampere = "ampere"
kelvin = "kelvin"
kilogram = "kilogram"
meter = "meter"
second = "second"

pi = float(pi) -- use numerical value of pi

H
P2
V
</textarea>
</td><td>
<button onclick="run()" style="font-size:20pt">Run</button>
</td></tr></table>

<p>
<div id="stdout"></div>

</body>
</html>
