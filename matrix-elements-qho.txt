-- Matrix elements for quantum harmonic oscillator

psi(n) = (m omega / (pi hbar))^(1/4) *
         exp(-1/2 m omega x^2 / hbar) *
         1 / sqrt(2^n n!) *
         Hermite(n, sqrt(m omega / hbar) x)

Hermite(n,f) = eval((-1)^n exp(x^2) d(exp(-x^2),x,n), x, f)

Hhat(psi) = phat(phat(psi)) / (2 m) + 1/2 m omega^2 x^2 psi

phat(psi) = -i hbar d(psi,x)

E(n) = hbar omega (n + 1/2)

psi0 = psi(0)
psi1 = psi(1)
psi2 = psi(2)
psi3 = psi(3)

-- integral of f from -inf to +inf

I(f) = do(
  f = integral(f,x),
  f = eval(f, exp(-m omega x^2 / hbar), 0),
  eval(f, erf(sqrt(m omega / hbar) x), 1) -
  eval(f, erf(sqrt(m omega / hbar) x), -1)
)

X = ((I(conj(psi0) x psi0),
      I(conj(psi0) x psi1),
      I(conj(psi0) x psi2),
      I(conj(psi0) x psi3)),

     (I(conj(psi1) x psi0),
      I(conj(psi1) x psi1),
      I(conj(psi1) x psi2),
      I(conj(psi1) x psi3)),

     (I(conj(psi2) x psi0),
      I(conj(psi2) x psi1),
      I(conj(psi2) x psi2),
      I(conj(psi2) x psi3)),

     (I(conj(psi3) x psi0),
      I(conj(psi3) x psi1),
      I(conj(psi3) x psi2),
      I(conj(psi3) x psi3)))

P = ((I(conj(psi0) phat(psi0)),
      I(conj(psi0) phat(psi1)),
      I(conj(psi0) phat(psi2)),
      I(conj(psi0) phat(psi3))),

     (I(conj(psi1) phat(psi0)),
      I(conj(psi1) phat(psi1)),
      I(conj(psi1) phat(psi2)),
      I(conj(psi1) phat(psi3))),

     (I(conj(psi2) phat(psi0)),
      I(conj(psi2) phat(psi1)),
      I(conj(psi2) phat(psi2)),
      I(conj(psi2) phat(psi3))),

     (I(conj(psi3) phat(psi0)),
      I(conj(psi3) phat(psi1)),
      I(conj(psi3) phat(psi2)),
      I(conj(psi3) phat(psi3))))

X
P

H = dot(P,P) / (2 m) + 1/2 m omega^2 dot(X,X)

H[4,4] = E(3) -- correction for finite matrices

H

c0 = x0 + i y0
c1 = x1 + i y1
c2 = x2 + i y2
c3 = x3 + i y3

Psi = (c0,c1,c2,c3)

dot(conj(Psi),X,Psi) ==
(conj(c0) c1 + conj(c1) c0) X[1,2] +
(conj(c1) c2 + conj(c2) c1) X[2,3] +
(conj(c2) c3 + conj(c3) c2) X[3,4]
