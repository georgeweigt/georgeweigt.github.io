-- Draw black-body spectrum for 3000 Kelvin (4940 Fahrenheit)

"Planck's law"

rho = 2 h nu^3 / c^2 / (exp(h nu / k / T) - 1)
rho

T = 3000 kelvin
c = 299792458 meter / second
h = 6.62607015 10^(-34) joule second -- exact since 2019 definition of kilogram
k = 1.38064852 10^(-23) joule / kelvin

joule = kilogram meter^2 / second^2

"At 3000 K we have (in joules per square meter)"

rho = eval(rho, nu, nu / second) -- replace nu with nu per second
rho = rho meter^2 / joule -- cancel physical units
rho

-- x axis: radiant frequency (hertz)
-- y axis: radiant exposure (joules per square meter)

xrange = (0,10^15)
yrange = (0,10^(-8))

"Radiant energy (rho) vs frequency (nu)"

draw(rho,nu)
