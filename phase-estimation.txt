run("Downloads/q4.txt")

-- theta is the phase to estimate

theta = 0.3

-- use numerical value of pi

pi = float(pi)

-- U is unitary transform

U = dot(
P23(2 pi theta),
P23(2 pi theta),
P23(2 pi theta),
P23(2 pi theta),
P13(2 pi theta),
P13(2 pi theta),
P03(2 pi theta))

-- V is inverse QFT of bits 0-2

V = dot(
H2,
P02(-pi/4),
P12(-pi/2),
H1,
P01(-pi/2),
H0,
S02)

-- psi is amplitude

psi = dot(V,U,X3,H2,H1,H0,ket0)

-- P is probability

P = hadamard(psi,conj(psi))

xrange = (0,16)
yrange = (0,1)
draw(P[ceiling(x)],x)

"Expectation value"
1/8 sum(k,1,16,(k - 1) P[k]) - 1
