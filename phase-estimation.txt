-- Phase estimation demo

-- See https://qiskit.org/textbook/ch-algorithms/quantum-phase-estimation.html

run("Downloads/q4.txt")

"Theta is the value to estimate (range 0.0 to 1.0)"

theta = 0.7
theta

pi = float(pi) -- use numerical value of pi

U = dot(
P3(16 pi theta),
P2(8 pi theta),
P1(4 pi theta),
P0(2 pi theta))

psi = dot(R,U,H3,H2,H1,H0,ket0)

"Probability distribution"

P = hadamard(psi,conj(psi))

xrange = (0,16)
yrange = (0,1)
draw(P[ceiling(x)],x)

"Expectation value (estimate of theta)"
1/16 sum(k,0,15,k P[k + 1])
