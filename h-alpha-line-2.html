<html><head><link rel='stylesheet' href='style.css'></head><body><script src='https://georgeweigt.github.io/eigenmath.js'></script><textarea id='stdin'>
-- Transition probabilities for H-alpha

-- psi returns a hydrogen atom eigenfunction

psi(n,l,m) = R(n,l) Y(l,m)

-- R returns a radial eigenfunction
 
R(n,l) = 2 / n^2 *
         a0^(-3/2) *
         sqrt((n - l - 1)! / (n + l)!) *
         (2 r / (n a0))^l *
         L(2 r / (n a0),n - l - 1,2 l + 1) *
         exp(-r / (n a0))

L(x,n,m) = (n + m)! sum(k,0,n,(-x)^k / ((n - k)! (m + k)! k!))

-- Y returns a spherical harmonic eigenfunction

Y(l,m) = (-1)^m sqrt((2l + 1) / (4 pi) (l - m)! / (l + m)!) *
         P(l,m) exp(i m phi)

-- associated legendre of cos theta

P(l,m) = test(m < 0, (-1)^m (l + m)! / (l - m)! P(l,-m),
         1 / (2^l l!) sin(theta)^m *
         eval(d((x^2 - 1)^l,x,l + m),x,cos(theta)))

-- E(n) returns the nth energy eigenvalue

E(n) = -e^2 / (8 pi epsilon0 a0 n^2)

-- integrate f

I(f) = do(
  f = f r^2 sin(theta), -- multiply by volume element
  f = circexp(f), -- convert to exponential form
  f = defint(f,theta,0,pi,phi,0,2pi),
  f = integral(f,r),
  0 - eval(f,r,0) -- return value
)

X(fk,fi) = do(
  xki = I(conj(fk) r sin(theta) cos(phi) fi),
  yki = I(conj(fk) r sin(theta) sin(phi) fi),
  zki = I(conj(fk) r cos(theta) fi),
  float(conj(xki) xki + conj(yki) yki + conj(zki) zki) -- return value
)

"2p 3s"

(X(psi(2,1,1),psi(3,0,0)),
 X(psi(2,1,0),psi(3,0,0)),
 X(psi(2,1,-1),psi(3,0,0)))

"2s 3p"

(X(psi(2,0,0),psi(3,1,1)),
 X(psi(2,0,0),psi(3,1,0)),
 X(psi(2,0,0),psi(3,1,-1)))

"2p 3d"

((X(psi(2,1,1),psi(3,2,2)),
  X(psi(2,1,0),psi(3,2,2)),
  X(psi(2,1,-1),psi(3,2,2))),

 (X(psi(2,1,1),psi(3,2,1)),
  X(psi(2,1,0),psi(3,2,1)),
  X(psi(2,1,-1),psi(3,2,1))),

 (X(psi(2,1,1),psi(3,2,0)),
  X(psi(2,1,0),psi(3,2,0)),
  X(psi(2,1,-1),psi(3,2,0))),

 (X(psi(2,1,1),psi(3,2,-1)),
  X(psi(2,1,0),psi(3,2,-1)),
  X(psi(2,1,-1),psi(3,2,-1))),

 (X(psi(2,1,1),psi(3,2,-2)),
  X(psi(2,1,0),psi(3,2,-2)),
  X(psi(2,1,-1),psi(3,2,-2))))
</textarea> <button onclick='run()'>Run</button><br><div id='stdout'></div></body></html>
