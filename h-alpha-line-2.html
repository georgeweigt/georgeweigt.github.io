<html><body><script src='https://georgeweigt.github.io/eigenmath.js'></script><textarea id='stdin' rows='24' cols='80' style='font-family:courier;font-size:12pt'>
-- Transition probabilities for H-alpha

-- psi returns a hydrogen atom eigenfunction

psi(n,l,m) = R(n,l) Y(l,m)

-- R returns a radial eigenfunction
 
R(n,l) = 2 / n^2 *
         a0^(-3/2) *
         sqrt((n - l - 1)! / (n + l)!) *
         (2 r / (n a0))^l *
         L(2 r / (n a0),n - l - 1,2 l + 1) *
         exp(-r / (n a0))

L(x,n,m) = (n + m)! sum(k,0,n,(-x)^k / ((n - k)! (m + k)! k!))

-- Y returns a spherical harmonic eigenfunction

Y(l,m) = (-1)^m sqrt((2l + 1) / (4 pi) (l - m)! / (l + m)!) *
         P(cos(theta),l,m) exp(i m phi)

P(f,n,m) = eval(1 / (2^n n!) (1 - x^2)^(m/2) *
           d((x^2 - 1)^n,x,n + m),x,f)

-- E(n) returns the nth energy eigenvalue

E(n) = -e^2 / (8 pi epsilon0 a0 n^2)

-- integrate f

I(f) = do(
  f = f r^2 sin(theta), -- multiply by volume element
  f = eval(f, sqrt(1 - cos(theta)^2),        sin(theta)),       -- simplify
  f = eval(f, 1 / sqrt(1 - cos(theta)^2),    1 / sin(theta)),   -- simplify
  f = eval(f, 1 / (1 - cos(theta)^2),        1 / sin(theta)^2), -- simplify
  f = eval(f, 1 / (1 - cos(theta)^2)^(3/2),  1 / sin(theta)^3), -- simplify
  f = defint(f,theta,0,pi,phi,0,2pi),
  f = integral(f,r),
  0 - eval(f,r,0)
)

X(fk,fi) = do(
  ax = I(conj(fk) r sin(theta) cos(phi) fi),
  ay = I(conj(fk) r sin(theta) sin(phi) fi),
  az = I(conj(fk) r cos(theta) fi),
  float(conj(ax) ax + conj(ay) ay + conj(az) az)
)

"2p 3s"

(X(psi(2,1,1),psi(3,0,0)),
 X(psi(2,1,0),psi(3,0,0)),
 X(psi(2,1,-1),psi(3,0,0)))

"2s 3p"

(X(psi(2,0,0),psi(3,1,1)),
 X(psi(2,0,0),psi(3,1,0)),
 X(psi(2,0,0),psi(3,1,-1)))

"2p 3d"

((X(psi(2,1,1),psi(3,2,2)),
  X(psi(2,1,0),psi(3,2,2)),
  X(psi(2,1,-1),psi(3,2,2))),

 (X(psi(2,1,1),psi(3,2,1)),
  X(psi(2,1,0),psi(3,2,1)),
  X(psi(2,1,-1),psi(3,2,1))),

 (X(psi(2,1,1),psi(3,2,0)),
  X(psi(2,1,0),psi(3,2,0)),
  X(psi(2,1,-1),psi(3,2,0))),

 (X(psi(2,1,1),psi(3,2,-1)),
  X(psi(2,1,0),psi(3,2,-1)),
  X(psi(2,1,-1),psi(3,2,-1))),

 (X(psi(2,1,1),psi(3,2,-2)),
  X(psi(2,1,0),psi(3,2,-2)),
  X(psi(2,1,-1),psi(3,2,-2))))
</textarea><p><button onclick='run()'>Run</button><p><div id='stdout'></div></body></html>
