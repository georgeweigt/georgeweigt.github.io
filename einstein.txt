-- Verify Einstein's equilibrium formula for Lyman-alpha line

h = 2 pi hbar

e = quote(e) -- clear e (needed for elementary charge)

-- Planck's law

rho(nu) = 2 h nu^3 / (c^2 (exp(h nu / (k T)) - 1))

-- E(n) is the nth energy level for hydrogen

E(n) = -e^2 / (8 pi epsilon0 a0 n^2)

-- nu is transition frequency

nu = (E(2) - E(1)) / h

-- spontaneous emission

A21 = e^10 me / (26244 pi^5 epsilon0^5 hbar^6 c^3)

-- induced emission

B21 = c^2 / (2 h nu^3) A21

-- absorption

p1 = 1
p2 = 3

B12 = p2 / p1 B21

"Coefficients for Lyman-alpha line"

A21
B21
B12

"Verify Einstein equilibrium formula (1=ok)"

B12 rho(nu) p1 exp(-E(1) / (k T)) ==
B21 rho(nu) p2 exp(-E(2) / (k T)) + A21 p2 exp(-E(2) / (k T))
