-- Feynman and Hibbs problem 4-2

e = quote(e)
x = (x1,x2,x3)
eta = (eta1,eta2,eta3)
A = (A1(),A2(),A3())

T = i e / (hbar c) dot(eta,A)

D(f) = dot(eta,d(f,x))

I = sqrt(2 pi i hbar epsilon / m)
J = 0
K = i hbar epsilon / m sqrt(2 pi i hbar epsilon / m)

-- Volume integral

V(f) = do(
  f = I^3 f,
  f = eval(f, eta1^2, K / I),
  f = eval(f, eta2^2, K / I),
  f = eval(f, eta3^2, K / I),
  f = eval(f, eta1, J / I),
  f = eval(f, eta2, J / I),
  f = eval(f, eta3, J / I),
  f
)

"Verify equation (7)"

B = V(psi())
C = (2 pi i hbar epsilon / m)^(3/2) psi()
check(B == C)
"ok"

"Verify equation (8)"

B = V(D(psi()))
check(B == 0)
"ok"

"Verify equation (9)"

B = V(1/2 D(D(psi())))
C = i hbar epsilon / (2 m) *
    (2 pi i hbar epsilon / m)^(3/2) *
    (d(psi(),x1,x1) + d(psi(),x2,x2) + d(psi(),x3,x3))
check(B == C)
"ok"

"Verify equation (10)"
B = V(T psi())
check(B == 0)
"ok"

"Verify equation (11)"

B = V(T D(psi()))
C = i hbar epsilon / m *
    (2 pi i hbar epsilon / m)^(3/2) *
    i e / (hbar c) *
    dot(A,d(psi(),x))
check(B == C)
"ok"

"Verify equation (12)"

B = V(T 1/2 D(D(psi())))
check(B == 0)
"ok"

"Verify equation (13)"

B = V(1/2 T^2 psi())
C = i hbar epsilon / m *
    (2 pi i hbar epsilon / m)^(3/2) *
    (-e^2 / (2 hbar^2 c^2)) *
    dot(A,A) psi()
check(B == C)
"ok"

"Verify equation (14)"

B = V(1/2 T^2 D(psi))
check(B == 0)
"ok"

"Verify equation (15)"

B = V(1/2 T^2 1/2 D(D(psi)))
check(B == 0)
"ok"

"Verify equation (?)"

f = (1 + T + 1/2 T^2) (psi() + D(psi()) + 1/2 D(D(psi())))

B = V(f)

B == V(psi()) +
     0 V(D(psi()))+
     V(1/2 D(D(psi()))) +

     0 V(T psi()) +
     V(T D(psi()))+
     0 V(T 1/2 D(D(psi()))) +

     V(1/2 T^2 psi()) +
     0 V(1/2 T^2 D(psi())) +
     V(1/2 T^2 1/2 D(D(psi())))



--B = eval(B,epsilon^(7/2),0)

C = psi() +
    i hbar epsilon / (2 m) *
    (d(psi(),x1,x1) + d(psi(),x2,x2) + d(psi(),x3,x3)) +
    i hbar epsilon / m i e / (hbar c) dot(A,d(psi(),x)) +
    i hbar epsilon / m (-e^2 / (2 hbar^2 c^2)) dot(A,A) psi()

C = C (2 pi i hbar epsilon / m)^(3/2) + V(1/2 T^2 1/2 D(D(psi())))


check(B == C)

"ok"

Y = V(1/2 T^2 1/2 D(D(psi())))
Y

Z = dot(d(psi(),x,x),A,A) -
    A1()^2 d(psi(),x1,x1) -
    A2()^2 d(psi(),x2,x2) -
    A3()^2 d(psi(),x3,x3)

Z = Z (2 pi i hbar epsilon / m)^(3/2) / c^2 / m^2 / 2 epsilon^2 e^2
Z



"Verify equation (16)"

T = i e / (hbar c) dot(eta,A)

f = (1 + T + 1/2 T^2) (psi() + D(psi()) + 1/2 D(D(psi())))

B = V(f) (1 - i e epsilon / hbar phi())

B = eval(B,epsilon^(9/2),0)
B = eval(B,epsilon^(7/2),0)

C = (hbar / i)^2 (d(psi(),x1,x1) + d(psi(),x2,x2) + d(psi(),x3,x3)) +
    2 (hbar / i) (e / c) dot(d(psi(),x),A) +
    (e / c)^2 dot(A,A) psi()

C = -i / hbar (1 / (2 m) C + e phi() psi())

C = (2 pi i hbar epsilon / m)^(3/2) (epsilon C + psi())

check(B == C)

"ok"
