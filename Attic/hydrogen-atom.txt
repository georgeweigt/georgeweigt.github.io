-- www.eigenmath.org/hydrogen-atom.txt

-- Laplacian operator for spherical coordinates

L(f) = 1/r^2 d(r^2 d(f,r),r) +
       1/(r^2 sin(theta)) d(sin(theta) d(f,theta),theta) +
       1/(r sin(theta))^2 d(d(f,phi),phi)

e = quote(e) -- clear e

rho(n) = mu e^2 / (2 pi epsilon0 hbar^2) * 1/n

R(n,l) = sqrt((n-l-1)!/(2n (n+l)!)) *
         r^l rho(n)^(l+3/2) *
         exp(-1/2 r rho(n)) laguerre(r rho(n),n-l-1,2l+1)

Y(l,m) = 1/2 (-1)^m sqrt((2l+1)/pi) *
         sqrt((l-abs(m))!/(l+abs(m))!) *
         exp(i m phi) legendre(cos(theta),l,abs(m))

H(psi) = -hbar^2/(2 mu) L(psi) - e^2 / (4 pi epsilon0 r) psi

E = -mu e^4 / (32 pi^2 epsilon0^2 hbar^2) * 1/n^2

-- check with schrodinger equation

for(n,1,4,for(l,0,n-1,for(m,-l,l,
  psi = R(n,l) Y(l,m),
  Z = H(psi) - E psi,
  Z = simplify(Z),
  check(Z=0)
)))

-- print a wave function

psi100 = R(1,0) Y(0,0)
psi100

-- more checks

a0 = 4 pi epsilon0 hbar^2 / (e^2 mu)

check(R(1,0) = 2 a0^(-3/2) exp(-r/a0))
check(R(2,0) = 2 (2a0)^(-3/2) (1 - r/2/a0) exp(-r/2/a0))
check(R(2,1) = 1/sqrt(3) (2a0)^(-3/2) r/a0 exp(-r/2/a0))
check(R(3,0) = 2 (3a0)^(-3/2) (1 - 2/3 r/a0 + 2/27 (r/a0)^2) exp(-r/3/a0))
check(R(3,1) = 4/9 sqrt(2) (3a0)^(-3/2) r/a0 (1 - r/6/a0) exp(-r/3/a0))
check(R(3,2) = 2/27 sqrt(2/5) (3a0)^(-3/2) (r/a0)^2 exp(-r/3/a0))

check(Y(0,0) = 1/sqrt(4pi))
check(Y(1,0) = sqrt(3/4/pi) cos(theta))
check(Y(1,1) = sqrt(3/8/pi) sin(theta) exp(i phi))
check(Y(1,-1) = sqrt(3/8/pi) sin(theta) exp(-i phi))
check(Y(2,0) = sqrt(5/16/pi) (3 cos(theta)^2 - 1))
check(Y(2,1) = sqrt(15/8/pi) sin(theta) cos(theta) exp(i phi))
check(Y(2,-1) = sqrt(15/8/pi) sin(theta) cos(theta) exp(-i phi))
check(Y(2,2) = sqrt(15/32/pi) sin(theta)^2 exp(2 i phi))
check(Y(2,-2) = sqrt(15/32/pi) sin(theta)^2 exp(-2 i phi))

"ok"
