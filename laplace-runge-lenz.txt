"Verify quantum LRL operators (1=ok)"

R = sqrt(x^2 + y^2 + z^2)

-- linear momentum operators

Px(f) = -i hbar d(f,x)
Py(f) = -i hbar d(f,y)
Pz(f) = -i hbar d(f,z)

-- angular momentum operators

Lx(f) = y Pz(f) - z Py(f)
Ly(f) = z Px(f) - x Pz(f)
Lz(f) = x Py(f) - y Px(f)

-- Laplace-Runge-Lenz (LRL) operators

Ax(f) = 1 / (2 m) *
        (Py(Lz(f)) - Pz(Ly(f)) - Ly(Pz(f)) + Lz(Py(f))) -
        Z / R x f

Ay(f) = 1 / (2 m) *
        (Pz(Lx(f)) - Px(Lz(f)) - Lz(Px(f)) + Lx(Pz(f))) -
        Z / R y f

Az(f) = 1 / (2 m) *
        (Px(Ly(f)) - Py(Lx(f)) - Lx(Py(f)) + Ly(Px(f))) -
        Z / R z f

-- squared operators

P2(f) = Px(Px(f)) + Py(Py(f)) + Pz(Pz(f))
L2(f) = Lx(Lx(f)) + Ly(Ly(f)) + Lz(Lz(f))
A2(f) = Ax(Ax(f)) + Ay(Ay(f)) + Az(Az(f))

-- hamiltonian operator

H(f) = 1 / (2 m) P2(f) - Z / R f

-- psi is a generic wave function

psi = f(x,y,z)

Ax(Lx(psi)) + Ay(Ly(psi)) + Az(Lz(psi)) == 0

Lx(Ax(psi)) + Ly(Ay(psi)) + Lz(Az(psi)) == 0

H(Ax(psi)) - Ax(H(psi)) == 0
H(Ay(psi)) - Ay(H(psi)) == 0
H(Az(psi)) - Az(H(psi)) == 0

A2(psi) == 2 / m H(L2(psi) + hbar^2 psi) + Z^2 psi
