-- Verify formulas

Delta = omega2 - omega1
f = expsin(omega tprime) exp(i Delta tprime)
c2 = M21 / (i hbar) defint(f, tprime, 0, t)

c21 = (exp(i (Delta - omega) t) - 1) / (Delta - omega)
c22 = (exp(i (Delta + omega) t) - 1) / (Delta + omega)

"Verify integral (1)"
check(c2 == M21 / (2 i hbar) (c21 - c22))
"ok"

c2 = M21 / (2 i hbar) c21

sinc(x) = expsin(x) / x

c2prime = M21 t / (2 hbar) *
 exp(i (Delta - omega) / 2 t) *
 sinc((Delta - omega) / 2 t)

"Verify sinc form (2)"
check(c2 == c2prime)
"ok"

c2 = M21 / (2 hbar) t exp(i (Delta - omega) / 2 t)
sinc2 = 2 pi / t delta(Delta - omega)
P = conj(c2) c2 sinc2

"Verify equation (3)"
check(P == pi / (2 hbar^2) M21^2 t delta(Delta - omega))
"ok"

-- eigenstates for infinite square well potential

psi1 = sqrt(2 / L) sin(pi x / L)
psi2 = sqrt(2 / L) sin(2 pi x / L)

-- verify eigenstates

H(psi) = -hbar^2 / (2 m) d(psi,x,2)
E(n) = n^2 pi^2 hbar^2 / (2 m L^2)

check(H(psi1) == E(1) psi1)
check(H(psi2) == E(2) psi2)

-- convert to exp form to integrate

psi1 = circexp(psi1)
psi2 = circexp(psi2)

V = (x - L / 4)^2

M21 = defint(conj(psi2) V psi1, x, 0, L)

"Verify matrix element (4)"
check(M21 == -8 L^2 / (9 pi^2))
"ok"

"Transition rate"

d(P,t)
