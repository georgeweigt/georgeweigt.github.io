-- Feynman and Hibbs problem 4-1

x = (x1,x2,x3)
eta = (eta1,eta2,eta3)

-- Directional derivative (unnormalized)

D(f) = dot(eta,d(f,x))

-- Volume integral of exp(...) f

integral3(f) = do(
  f = f (2 pi i hbar epsilon / m)^(3/2),
  f = eval(f, eta1^2, i hbar epsilon / m),
  f = eval(f, eta2^2, i hbar epsilon / m),
  f = eval(f, eta3^2, i hbar epsilon / m),
  f = eval(f, eta1, 0),
  f = eval(f, eta2, 0),
  f = eval(f, eta3, 0),
  f
)

"Verify equation (13)"

A = integral3(psi())
B = (2 pi i hbar epsilon / m)^(3/2) psi()
check(A == B)
"ok"

"Verify equation (14)"

A = integral3(D(psi()))
check(A == 0)

"ok"

"Verify equation (15)"

A = integral3(D(D(psi())))
B = i hbar epsilon / m *
    (2 pi i hbar epsilon / m)^(3/2) *
    (d(psi(),x1,x1) + d(psi(),x2,x2) + d(psi(),x3,x3))
check(A == B)
"ok"

"Verify equation (16)"

f = psi() + D(psi()) + 1/2 D(D(psi()))
A = integral3(f)
A = A (1 - i epsilon / hbar V)
A = eval(A,epsilon^(7/2),0)
A = A / (2 pi i hbar epsilon / m)^(3/2)
A = A - psi()
A = A / epsilon
B = i hbar / (2 m) *
    (d(psi(),x1,x1) + d(psi(),x2,x2) + d(psi(),x3,x3)) +
    (-1) i / hbar V psi()
check(A == B)
"ok"

"Compare with Feynman and Hibbs equation (4.13)"

B = -hbar^2 / (2 m) *
    (d(psi(),x1,x1) + d(psi(),x2,x2) + d(psi(),x3,x3)) +
    V psi()

B = -i / hbar B

check(A == B)

"ok"
