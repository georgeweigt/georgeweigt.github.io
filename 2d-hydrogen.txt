-- Verify eigenfunctions for 2d hydrogen

E(n) = -k^2 mu / (2 hbar^2 (n + 1/2)^2)

-- range of m is -n...n

psi(n,m) =
  1 / a0 / sqrt(pi) *
  (n + 1/2)^(-3/2) *
  sqrt((n - abs(m))! / (n + abs(m))!) *
  (2 r / a0 / (n + 1/2))^abs(m) *
  Laguerre(2 r / a0 / (n + 1/2), n - abs(m), 2 abs(m)) *
  exp(-r / a0 / (n + 1/2)) *
  exp(i m phi)

a0 = hbar^2 / (k mu)

-- Laguerre polynomial (y is a local variable)

Laguerre(x,n,m,y) = eval(y^(-m) exp(y) / n! d(exp(-y) y^(n + m), y, n), y, x)

psi0 = psi(0,0)
psi1 = psi(1,0)
psi2 = psi(2,0)
psi3 = psi(3,0)
psi4 = psi(4,0)

psi0
psi1
psi2
psi3
psi4

"Verify Schroedinger equation (1=ok)"

Laplacian(psi) =
  d(psi,r,2) +
  d(psi,r) / r +
  d(psi,phi,2) / r^2

-hbar^2 / (2 mu) Laplacian(psi0) - k psi0 / r == E(0) psi0
-hbar^2 / (2 mu) Laplacian(psi1) - k psi1 / r == E(1) psi1
-hbar^2 / (2 mu) Laplacian(psi2) - k psi2 / r == E(2) psi2
-hbar^2 / (2 mu) Laplacian(psi3) - k psi3 / r == E(3) psi3
-hbar^2 / (2 mu) Laplacian(psi4) - k psi4 / r == E(4) psi4

"Verify normalization (1=ok)"

I(f) = do(
  f = f r, -- for integrating over polar coordinates
  f = defint(f, phi, 0, 2 pi),
  f = integral(f,r),
  0 - eval(f,r,0)
)

I(conj(psi0) psi0)
I(conj(psi1) psi1)
I(conj(psi2) psi2)
I(conj(psi3) psi3)
I(conj(psi4) psi4)
