-- Draw cdf for Compton scattering

-- number of bins

N = 10

-- incident energy

E = 10^5 eV
omega = E / hbar

joule = kilogram meter^2 / second^2

c = 299792458.0 meter / second
eV = 1.602176634 10^(-19) joule
h = 6.62607015 10^(-34) joule second
hbar = h / float(2 pi)
me = 9.1093837015 10^(-31) kilogram

R = hbar omega / (me c^2)

I = -R^2 / (-R^3 + R^3 cos(theta) - R^2) -
    R / (-R^3 + R^3 cos(theta) - R^2) +
    2 R / (-R^4 + R^4 cos(theta) - R^3) -
    1 / (R (R (-cos(theta) + 1) + 1)) -
    1 / (2 R (R (-cos(theta) + 1) + 1)^2) +
    log(R - R cos(theta) + 1) / R -
    cos(theta) / R^2 -
    2 log(R - R cos(theta) + 1) / R^2 -
    2 log(R - R cos(theta) + 1) / R^3 +
    1 / (-R^4 + R^4 cos(theta) - R^3) -
    1 / (R (-cos(theta) + 1) + 1)

pi = float(pi) -- use numerical value of pi

F = (I - eval(I,theta,0)) / (eval(I,theta,pi) - eval(I,theta,0))

"Cumulative distribution function"

xrange = (0,pi)
yrange = (0,1)
draw(F,theta)

"Bin probability"

P = zero(N)
for(k,1,N, P[k] = eval(F,theta,k pi/N) - eval(F,theta,(k-1) pi/N))
h(x) = test(x <= 0, 0, x > N, 0, P[ceiling(x)])
xrange = (0,N)
yrange = (0,1)
draw(h,x)
