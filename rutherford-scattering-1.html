<!doctype html>
<html lang="en-us">
<head>
<meta charset="utf-8">
<title>Eigenmath Demo</title>
</head>
<body>

<script src="eigenmath.js"></script>

<table><tr><td>
<textarea id="stdin" rows="24" cols="80" style="font-family:courier;font-size:12pt">
-- Verify formulas for Rutherford scattering

-- eigenmath.org/rutherford-scattering-1.txt

E = sqrt(p^2 + m^2)

p1 = (E, 0, 0, p)

p2 = (E,
      p expsin(theta) expcos(phi),
      p expsin(theta) expsin(phi),
      p expcos(theta))

u11 = (E + m, 0, p1[4], p1[2] + i p1[3])
u12 = (0, E + m, p1[2] - i p1[3], -p1[4])

u21 = (E + m, 0, p2[4], p2[2] + i p2[3])
u22 = (0, E + m, p2[2] - i p2[3], -p2[4])

u1 = (u11,u12)
u2 = (u21,u22)

N = (E + m)^2

I = ((1,0,0,0),(0,1,0,0),(0,0,1,0),(0,0,0,1))

gmunu = ((1,0,0,0),(0,-1,0,0),(0,0,-1,0),(0,0,0,-1))

gamma0 = ((1,0,0,0),(0,1,0,0),(0,0,-1,0),(0,0,0,-1))
gamma1 = ((0,0,0,1),(0,0,1,0),(0,-1,0,0),(-1,0,0,0))
gamma2 = ((0,0,0,-i),(0,0,i,0),(0,i,0,0),(-i,0,0,0))
gamma3 = ((0,0,1,0),(0,0,0,-1),(-1,0,0,0),(0,1,0,0))

gamma = (gamma0,gamma1,gamma2,gamma3)

pslash1 = dot(p1,gmunu,gamma)
pslash2 = dot(p2,gmunu,gamma)

u2bar = dot(conj(u2),gamma0) -- adjoint of u2

"Sum over spin states"

S = 0

for(a,1,2,for(b,1,2,
  M = dot(u2bar[b],gamma0,u1[a]),  -- M is an amplitude
  f = M conj(M),                   -- f = |M|^2
  S = S + f
))

S = S / N -- normalize
S

"Casimir trick"

T = contract(dot(pslash2 + m I,gamma0,pslash1 + m I,gamma0))
T

"Does S equal T?"

test(S == T,"yes","no")

"Verify additional formulas (1=ok)"

T == 4 (E^2 + m^2 + p^2 expcos(theta))

q = p1 - p2
q4 = dot(q,gmunu,q)^2

q4 == 16 p^4 expsin(theta/2)^4
q4 == 4 p^4 (expcos(theta) - 1)^2
</textarea>
</td><td>
<button onclick="run()" style="font-size:20pt">Run</button>
</td></tr></table>

<p>
<div id="stdout"></div>

</body>
</html>
