<html><head><link rel='stylesheet' href='style.css'></head><body><script src='https://georgeweigt.github.io/eigenmath.js'></script><textarea id='stdin'>
-- Verify formulas for Fermi's golden rule

Delta = omega2 - omega1

f = expcos(omega tprime + phi) exp(i Delta tprime)

c2 = 2 M21 / (i hbar) defint(f,tprime,0,t)

c21 = -M21 / hbar *
      (exp(i (Delta - omega) t) - 1) / (Delta - omega) *
      exp(-i phi)

c22 = -M21 / hbar *
      (exp(i (Delta + omega) t) - 1) / (Delta + omega) *
      exp(+i phi)

"Verify integral (1)"
check(c2 == c21 + c22)
check(eval(f,tprime,t) == d(c2, t) i hbar / (2 M21))
"ok"

c2 = c21

sinc(x) = expsin(x) / x

c2prime = -i t / hbar M21 *
          exp(i (Delta - omega) / 2 t - i phi) *
          sinc((Delta - omega) / 2 t)

"Verify sinc form (2)"
check(c2 == c2prime)
"ok"

sinc = quote(sinc) -- clear sinc def

c2 = -i t / hbar M21 *
     exp(i (Delta - omega) / 2 t - i phi) *
     sinc((Delta - omega) / 2 t)

P = conj(c2) c2

Pprime = t^2 / hbar^2 M21^2 sinc((Delta - omega) / 2 t)^2

"Verify equation (3)"
check(P == Pprime)
"ok"
</textarea> <button onclick='run()'>Run</button><br><div id='stdout'></div></body></html>
